plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.team3'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
    toolVersion = "0.8.11"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
    /* ==================================================
     * Core / Web
     * ================================================== */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    /* ==================================================
     * Security / OAuth2 / JWT
     * ================================================== */
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly  'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly  'io.jsonwebtoken:jjwt-jackson:0.13.0'

    /* ==================================================
     * Persistence
     * ================================================== */
    // JPA (PostgreSQL)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly  'org.postgresql:postgresql'

    // MongoDB
    // implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    /* ==================================================
     * QueryDSL
     * ================================================== */
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    /* ==================================================
     * Cache
     * ================================================== */
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    /* ==================================================
     * WebSocket / Messaging
     * ================================================== */
    // implementation 'org.springframework.boot:spring-boot-starter-websocket'

    /* ==================================================
     * Push Notification
     * ================================================== */
    implementation 'com.google.firebase:firebase-admin:9.7.0'
    implementation 'nl.martijndwars:web-push:5.1.2'

    /* ==================================================
     * Observability / API Docs
     * ================================================== */
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

    /* ==================================================
     * Developer Productivity
     * ================================================== */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    /* ==================================================
     * Test
     * ================================================== */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation platform("org.testcontainers:testcontainers-bom:1.19.8")
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:redis'
    // testImplementation 'org.testcontainers:mongodb'

    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
}

tasks.jacocoTestReport {
    dependsOn tasks.test

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

