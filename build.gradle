plugins {
    id 'org.springframework.boot' version '3.5.9' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.diffplug.spotless' version '7.0.0.BETA4' apply false
}

allprojects {
    group = 'com.tasteam'
    version = getVersionFromGit()
    description = 'Demo project for Spring Boot'

    repositories {
        mavenCentral()
    }
}

def getVersionFromGit() {
    def isCI = System.getenv('CI') == 'true'
    def currentBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    def isMainBranch = currentBranch == 'main'

    try {
        def allTags = 'git tag --points-at HEAD'.execute().text.trim().split('\n')
        def semanticTag = allTags.find { it.matches(/^v?\d+\.\d+\.\d+$/) }

        if (!semanticTag) {
            semanticTag = 'git describe --tags --abbrev=0'.execute().text.trim()
        }

        def commitCount = 'git rev-list --count HEAD'.execute().text.trim()
        def hash = 'git rev-parse --short HEAD'.execute().text.trim()

        if (semanticTag && !semanticTag.isEmpty() && semanticTag.matches(/^v?\d+\.\d+\.\d+$/)) {
            return "${semanticTag}.${commitCount}-${hash}"
        }

        if (isCI && isMainBranch) {
            throw new GradleException("main 브랜치의 CI/CD 환경에서는 시맨틱 버저닝 Git 태그(v1.0.0)가 필수입니다. 현재 태그: ${allTags.join(', ') ?: '없음'}")
        }

        return "0.0.1.${commitCount}-${hash}"
    } catch (Exception e) {
        if (isCI && isMainBranch) {
            throw new GradleException("main 브랜치의 CI/CD 환경에서 Git 버전 정보를 가져올 수 없습니다: ${e.message}")
        }
        return '0.0.1-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    jacoco {
        toolVersion = "0.8.11"
    }

    checkstyle {
        toolVersion = "10.12.7"
        configFile = file("$rootDir/config/checkstyle/naver-checkstyle-rules.xml")
        configProperties = [
                suppressionFile: file("$rootDir/config/checkstyle/naver-checkstyle-suppressions.xml"),
        ]
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    spotless {
        java {
            target 'src/**/*.java'
            // Eclipse 버전 명시 (HTTPS 프로토콜 문제 해결)
            eclipse('4.31').configFile(file("$rootDir/config/formatter/naver-eclipse-formatter.xml"))
            importOrder('java', 'javax', 'org', 'net', 'com')
            removeUnusedImports()
        }
    }
}

apply from: "${rootDir}/gradle/flyway/flywayCreate.gradle"
