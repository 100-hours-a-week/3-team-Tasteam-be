plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java-test-fixtures'
    id 'com.github.spotbugs' version '6.0.26'
}

dependencies {
    /* ==================================================
     * Core / Web
     * ================================================== */
    implementation "org.springframework.boot:spring-boot-starter-web:3.5.9"
    implementation "org.springframework.boot:spring-boot-starter-validation:3.5.9"

    /* ==================================================
     * Security / OAuth2 / JWT
     * ================================================== */
    implementation "org.springframework.boot:spring-boot-starter-security:3.5.9"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client:3.5.9"

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:0.13.0"
    runtimeOnly  "io.jsonwebtoken:jjwt-impl:0.13.0"
    runtimeOnly  "io.jsonwebtoken:jjwt-jackson:0.13.0"

    /* ==================================================
     * Persistence
     * ================================================== */
    // JPA (PostgreSQL)
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:3.5.9"
    runtimeOnly  "org.postgresql:postgresql:42.7.8"
    implementation "org.hibernate.orm:hibernate-spatial"

    // Flyway (DB Migration)
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-database-postgresql"

    // MongoDB
    // implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    /* ==================================================
     * QueryDSL
     * ================================================== */
    implementation "com.querydsl:querydsl-jpa:5.1.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api:2.1.1"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:3.1.0"

    /* ==================================================
     * Cache
     * ================================================== */
    implementation "org.springframework.boot:spring-boot-starter-cache:3.5.9"
    // implementation "org.springframework.boot:spring-boot-starter-data-redis:3.5.9"
    implementation "com.github.ben-manes.caffeine:caffeine:3.2.3"

    /* ==================================================
     * WebSocket / Messaging
     * ================================================== */
    // implementation 'org.springframework.boot:spring-boot-starter-websocket'

    /* ==================================================
     * Push Notification
     * ================================================== */
    implementation "com.google.firebase:firebase-admin:9.7.0"
    implementation "nl.martijndwars:web-push:5.1.2"

    /* ==================================================
     * Observability / API Docs
     * ================================================== */
    implementation "org.springframework.boot:spring-boot-starter-actuator:3.5.9"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15"
    implementation "net.logstash.logback:logstash-logback-encoder:9.0"
    implementation 'io.micrometer:micrometer-registry-prometheus'

    /* ==================================================
     * Storage (S3)
     * ================================================== */
    implementation "com.amazonaws:aws-java-sdk-s3:1.12.538"

    /* ==================================================
     * Image Processing
     * ================================================== */
    implementation "net.coobird:thumbnailator:0.4.20"
    implementation "org.sejda.imageio:webp-imageio:0.1.6"

    /* ==================================================
     * Developer Productivity
     * ================================================== */
    compileOnly "org.projectlombok:lombok:1.18.42"
    annotationProcessor "org.projectlombok:lombok:1.18.42"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:3.5.9"
    developmentOnly "org.springframework.boot:spring-boot-devtools:3.5.9"

    /* ==================================================
     * Test Fixtures
     * ================================================== */
    testFixturesImplementation "org.assertj:assertj-core:3.27.6"
    testFixturesImplementation "com.fasterxml.jackson.core:jackson-databind"
    testFixturesImplementation "org.hibernate.orm:hibernate-spatial"

    /* ==================================================
     * Test
     * ================================================== */
    testImplementation(testFixtures(project(":app-api")))
    testImplementation "org.springframework.boot:spring-boot-starter-test:3.5.9"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.12.0"

    testImplementation platform("org.testcontainers:testcontainers-bom:1.21.4")
    testImplementation "org.springframework.boot:spring-boot-testcontainers:3.5.9"
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    // testImplementation 'org.testcontainers:mongodb'

    testImplementation "org.assertj:assertj-core:3.27.6"
    testImplementation "org.mockito:mockito-core:5.17.0"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.12.2"

    // Find Security Bugs
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'
}

// ====== 테스트 설정 분리 ====== //

def calculateTestForks() {
    // 통합 테스트 컨텍스트가 동일 DB/Testcontainers를 공유하므로 병렬 실행을 1로 제한
    return 1
}

tasks.withType(Test).configureEach {
    systemProperty "spring.profiles.active", "test"
    maxParallelForks = calculateTestForks()
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'perf'
    }
    finalizedBy tasks.jacocoTestReport
}

tasks.register('unitTest', Test) {
    useJUnitPlatform {
        includeTags 'unit'
    }
    outputs.upToDateWhen { false }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform {
        includeTags 'integration'
    }
    outputs.upToDateWhen { false }
}

tasks.register('performanceTest', Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags 'perf'
    }
    outputs.upToDateWhen { false }
}

tasks.jacocoTestReport {
    dependsOn tasks.test

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

// SpotBugs 설정 (보안 체크)
spotbugs {
    ignoreFailures = true
    showStackTraces = true
    showProgress = true
    effort = 'max'
    reportLevel = 'low'
}

spotbugsMain {
    classDirs = files(sourceSets.main.output.classesDirs)
    auxClassPaths = configurations.compileClasspath + configurations.runtimeClasspath

    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            required = false
        }
    }
}

// ====== JaCoCo 설정 보강 ====== //

jacoco {
    toolVersion = "0.8.12"
}

def jacocoMinimumCoverage = 0.50

def configureJacocoClassDirs(task) {
    afterEvaluate {
        task.classDirectories.setFrom(files(task.classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/Q*.class',
                '**/infra/**',
                '**/swagger/**',
                '**/entity/id/**',
                '**/*Application.class',
                '**/*Config*.class',
                '**/common/aop/**',
                '**/dto/**',
                '**/*Dto*.class',
                '**/exception/**',
                '**/Client**'
            ])
        }))
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    configureJacocoClassDirs(it)

    executionData.setFrom(fileTree(buildDir).include("/jacoco/*.exec").exclude("/jacoco/performanceTest.exec"))
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn test
    configureJacocoClassDirs(it)
    violationRules {
        rule {
            limit {
                minimum = jacocoMinimumCoverage
            }
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
        xml {
            required = false
        }
    }
}
